!-----------------------------------------------------------------------------------------------!
SUBROUTINE GUESSA(NX,NY,ITYPE)  
!-----------------------------------------------------------------------------------------------!
!    Initial Guess                                                                              !
!-----------------------------------------------------------------------------------------------!
IMPLICIT DOUBLE PRECISION (A-H,O-Z)
!-----------------------------------------------------------------------------------------------!
PARAMETER(NDIM=300)
!-----------------------------------------------------------------------------------------------!
COMMON /COOR/  X(0:NDIM+1,0:NDIM+1),Y(0:NDIM+1,0:NDIM+1)
COMMON /AREA/  DSI(NDIM,4),DST(4),IDSIT(4),IAK(2),JAK(2)
COMMON /TRANS/ FXI(NDIM,NDIM),FETA(NDIM,NDIM)
!-----------------------------------------------------------------------------------------------!
NXM1=NX-1
NYM1=NY-1
DIVI=DFLOAT(NXM1)
DIVJ=DFLOAT(NYM1)
ZERO=1.0E-11
!-----------------------------------------------------------------------------------------------!
!    Grid Control                                                                               !
!-----------------------------------------------------------------------------------------------!
FETA(1 , 1)=0.0
FETA(1 ,NX)=0.0
FXI ( 1,1 )=0.0
FXI (NY,1 )=0.0
!-----------------------------------------------------------------------------------------------!
!    Eta Direction																	                                            !
!-----------------------------------------------------------------------------------------------!
DO J=2,NY
   JM1=J-1
   FETA(J,1 )=DSQRT((X(J,1 )-X(JM1,1 ))**2+(Y(J,1 )-Y(JM1,1 ))**2)
   FETA(J,NX)=DSQRT((X(J,NX)-X(JM1,NX))**2+(Y(J,NX)-Y(JM1,NX))**2)
END DO !J=2,NY
DO J=2,NY
   JM1=J-1
   FETA(J,1 )=FETA(JM1,1 )+FETA(J,1 )
   FETA(J,NX)=FETA(JM1,NX)+FETA(J,NX)
END DO !J=2,NY
!-----------------------------------------------------------------------------------------------!
DST(2)=FETA(NY, 1)
DST(4)=FETA(NY,NX)
IF (DST(2) > 1.D-12) THEN
   DO J=2,NYM1
      FETA(J,1)=FETA(J,1)/FETA(NY,1)
   END DO !J=2,NYM1
ELSE !(DST(2) > 1.D-12)
   DO J=1,NY
      FETA(J,1)=DFLOAT(J-1)/DFLOAT(NYM1)
   END DO !J=1,NY
END IF !(DST(2) > 1.D-12)
!-----------------------------------------------------------------------------------------------!
IF (DST(4) > 1.D-12) THEN
   DO J=2,NYM1
      FETA(J,NX)=FETA(J,NX)/FETA(NY,NX)
   END DO !J=2,NYM1
ELSE !(DST(4) > 1.D-12)
   DO J=1,NY
      FETA(J,NX)=DFLOAT(J-1)/DFLOAT(NYM1)
   END DO !J=1,NY
END IF !(DST(4) > 1.D-12)
!-----------------------------------------------------------------------------------------------!
!    Xi Direction                                                                               !
!-----------------------------------------------------------------------------------------------!
DO I=2,NX
   IM1=I-1
   FXI(1 ,I)=DSQRT((X(1 ,I)-X(1 ,IM1))**2+(Y(1 ,I)-Y(1 ,IM1))**2)
   FXI(NY,I)=DSQRT((X(NY,I)-X(NY,IM1))**2+(Y(NY,I)-Y(NY,IM1))**2)
END DO !I=2,NX
!-----------------------------------------------------------------------------------------------!
DO I=2,NX
   IM1=I-1
   FXI( 1,I)=FXI( 1,IM1)+FXI( 1,I)
   FXI(NY,I)=FXI(NY,IM1)+FXI(NY,I)
END DO !I=2,NX
!-----------------------------------------------------------------------------------------------!
DST(1)=FXI( 1,NX)
DST(3)=FXI(NY,NX)
!-----------------------------------------------------------------------------------------------!
IF (DST(1) > 1.D-12) THEN
   DO I=2,NXM1
      FXI(1,I)=FXI(1,I)/FXI(1,NX)
   END DO !I=2,NXM1
ELSE !(DST(1) > 1.D-12)
   DO I=2,NX
      FXI(1,I)=DFLOAT(I-1)/DFLOAT(NXM1)
   END DO !I=2,NX
END IF !(DST(1) > 1.D-12)
!-----------------------------------------------------------------------------------------------!
IF (DST(3) > 1.D-12) THEN
   DO I=2,NXM1
      FXI(NY,I)=FXI(NY,I)/FXI(NY,NX)
   END DO !I=2,NXM1
ELSE !(DST(3) > 1.D-12)
   DO I=2,NX
      FXI(NY,I)=FLOAT(I-1)/FLOAT(NXM1)
   END DO !I=2,NX
END IF !(DST(3) > 1.D-12)
!-----------------------------------------------------------------------------------------------!
!    Interpolation for the Interior Nodes                                                       !
!-----------------------------------------------------------------------------------------------!
DO I=2,NXM1
   RI=DFLOAT(I-1)/DIVI
   RIM1=1.0-RI
   DO J=2,NYM1
      RJ=DFLOAT(J-1)/DIVJ
      RJM1=1.0-RJ
      FXI (J,I)=FXI (1,I)*RJM1+FXI (NY,I)*RJ
      FETA(J,I)=FETA(J,1)*RIM1+FETA(J,NX)*RI 
   END DO !J=2,NYM1
END DO !I=2,NXM1
!-----------------------------------------------------------------------------------------------!
DO J=2,NYM1
   FXI(J,1 )=0.0
   FXI(J,NX)=1.0 
END DO !J=2,NYM1
!-----------------------------------------------------------------------------------------------!
DO I=2,NXM1
   FETA(1 ,I)=0.0
   FETA(NY,I)=1.0 
END DO !I=2,NXM1
!-----------------------------------------------------------------------------------------------!
!    Calculation of Distances at the Boundary                                                   !
!-----------------------------------------------------------------------------------------------!
!    Distances at the Corners														                                        !
!-----------------------------------------------------------------------------------------------!
IF (IDSIT(1) == 0) THEN
   DSI( 1,1)=DSQRT((X(2, 1)-X(1, 1))**2+(Y(2, 1)-Y(1, 1))**2)
   DSI(NX,1)=DSQRT((X(2,NX)-X(1,NX))**2+(Y(2,NX)-Y(1,NX))**2) 
END IF !(IDSIT(1) == 0)
IF (IDSIT(3) == 0) THEN
   DSI( 1,3)=DSQRT((X(NY, 1)-X(NYM1, 1))**2+(Y(NY, 1)-Y(NYM1, 1))**2)
   DSI(NX,3)=DSQRT((X(NY,NX)-X(NYM1,NX))**2+(Y(NY,NX)-Y(NYM1,NX))**2)
END IF !(IDSIT(3) == 0)
!-----------------------------------------------------------------------------------------------!
DO I=2,NXM1
   RI=FXI(1,I)
   RIM1=1.0-RI
   IF (IDSIT(1) == 0) DSI(I,1)=RIM1*DSI(1,1)+RI*DSI(NX,1)
!-----------------------------------------------------------------------------------------------!
   RI=FXI(NY,I)
   RIM1=1.0-RI
   IF (IDSIT(3) == 0) DSI(I,3)=RIM1*DSI(1,3)+RI*DSI(NX,3)
END DO !I=2,NXM1
!-----------------------------------------------------------------------------------------------!
!    Xi Lines                                                                                   !
!-----------------------------------------------------------------------------------------------!
IF (IDSIT(2) == 0) THEN
   DSI( 1,2)=DSQRT((X( 1,2)-X( 1,1))**2+(Y( 1,2)-Y( 1,1))**2)
   DSI(NY,2)=DSQRT((X(NY,2)-X(NY,1))**2+(Y(NY,2)-Y(NY,1))**2)
END IF !(IDSIT(2) == 0)
IF (IDSIT(4) == 0) THEN
   DSI( 1,4)=DSQRT((X( 1,NX)-X( 1,NXM1))**2+(Y( 1,NX)-Y( 1,NXM1))**2)
   DSI(NY,4)=DSQRT((X(NY,NX)-X(NY,NXM1))**2+(Y(NY,NX)-Y(NY,NXM1))**2)
END IF !(IDSIT(4) == 0)
!-----------------------------------------------------------------------------------------------!
DO J=2,NYM1
   RJ  =FETA(J,1)
   RJM1=1.0-RJ
   IF (IDSIT(2) == 0) DSI(J,2)=RJM1*DSI(1,2)+RJ*DSI(NY,2)
!-----------------------------------------------------------------------------------------------!
   RJ  =FETA(J,NX)
   RJM1=1.0-RJ
   IF (IDSIT(4) == 0) DSI(J,4)=RJM1*DSI(1,4)+RJ*DSI(NY,4)
END DO !J=2,NYM1
!-----------------------------------------------------------------------------------------------!
IF (ITYPE < 0) RETURN
!-----------------------------------------------------------------------------------------------!
!    Initial Grid                                                                               !
!																					                                                      !
!    ITYPE=0 or ITYPE<0 or ITYPE>1 --> Transfinite Interpolation                                !
!-----------------------------------------------------------------------------------------------!
IF ((ITYPE == 0).OR.(ITYPE >= 4)) THEN
   DO I=2,NXM1
      DO J=2,NYM1
!-----------------------------------------------------------------------------------------------!
         FAXI=1.D0-FXI(J,I)
         FBXI=FXI(J,I)
         FAETA=1.D0-FETA(J,I)
         FBETA=FETA(J,I)
!-----------------------------------------------------------------------------------------------!
         X0=FAXI*X(J,1)+FBXI*X(J,NX)+FAETA*X(1,I)+FBETA*X(NY,I)
         Y0=FAXI*Y(J,1)+FBXI*Y(J,NX)+FAETA*Y(1,I)+FBETA*Y(NY,I)
!-----------------------------------------------------------------------------------------------!
         XE=FAXI*(FAETA*X(1, 1)+FBETA*X(NY, 1))+FBXI*(FAETA*X(1,NX)+FBETA*X(NY,NX))
         YE=FAXI*(FAETA*Y(1, 1)+FBETA*Y(NY, 1))+FBXI*(FAETA*Y(1,NX)+FBETA*Y(NY,NX))
!-----------------------------------------------------------------------------------------------!
         X(J,I)=X0-XE
         Y(J,I)=Y0-YE
      END DO !J=2,NYM1
   END DO !I=2,NXM1
END IF !((ITYPE == 0).OR.(ITYPE >= 4))
!-----------------------------------------------------------------------------------------------!
!    ITYPE=1 --> Linear Interpolation on Xi                                                     !
!-----------------------------------------------------------------------------------------------!
IF (ITYPE == 1) THEN
   DO J=2,NYM1
      DO I=2,NXM1
         FAXI=1.D0-FXI(J,I)
         FBXI=FXI(J,I)
!-----------------------------------------------------------------------------------------------!
         X(J,I)=FAXI*X(J,1)+FBXI*X(J,NX)
         Y(J,I)=FAXI*Y(J,1)+FBXI*Y(J,NX)
      END DO !I=2,NXM1
   END DO !J=2,NYM1
END IF !(ITYPE == 1)
!-----------------------------------------------------------------------------------------------!
!    ITYPE=2 --> Linear Interpolation on Eta                                                    !
!-----------------------------------------------------------------------------------------------!
IF (ITYPE == 2) THEN
   DO I=2,NXM1
      DO J=2,NYM1
         FAETA=1.D0-FETA(J,I)
         FBETA=FETA(J,I)
!-----------------------------------------------------------------------------------------------!
         X(J,I)=FAETA*X(1,I)+FBETA*X(NY,I)
         Y(J,I)=FAETA*Y(1,I)+FBETA*Y(NY,I)
      END DO !J=2,NYM1
   END DO !I=2,NXM1
END IF !(ITYPE == 2)
!-----------------------------------------------------------------------------------------------!
!    ITYPE=3 --> Bilinear Interpolation                                                         !
!-----------------------------------------------------------------------------------------------!
IF (ITYPE == 3) THEN
   DO I=2,NXM1
      DO J=2,NYM1
         AX  =2.D0* FXI(J,I)-1.D0
         AY  =2.D0*FETA(J,I)-1.D0
         AXY =AX*AY-1.D0
         DAXY=AX-AY
         DSQ2=DSQRT(AXY**2-DAXY**2)
         DSXY=DAXY+ZERO
         XI  =(DSIGN(DSQ2,AXY)-AXY)/DSXY
         ETA =(AX+AY)/(2.D0-DAXY*XI+ZERO)
!-----------------------------------------------------------------------------------------------!
         PHIA=(1.D0+XI)*(1.D0-ETA)
         PHIB=(1.D0+XI)*(1.D0+ETA)
         PHIC=(1.D0-XI)*(1.D0+ETA)
         PHID=(1.0-XI)*(1.0-ETA)
         X(J,I)=0.25D0*(X(1,I)*PHIA+X(NY,I)*PHIC+X(J,1)*PHID+X(J,NX)*PHIB)
         Y(J,I)=0.25D0*(Y(1,I)*PHIA+Y(NY,I)*PHIC+Y(J,1)*PHID+Y(J,NX)*PHIB)
      END DO !J=2,NYM1
   END DO !I=2,NXM1
END IF !(ITYPE == 3)
!-----------------------------------------------------------------------------------------------!
CALL BORD(NX,NY)
CALL ANGRI(NX,NY,0)
!-----------------------------------------------------------------------------------------------!
1000 FORMAT('  Divergence of Newton iteration at (',I3,',',I3,').',/, &
            '  Last corrections :',1X,1PE13.6,1X,1PE13.6)
!-----------------------------------------------------------------------------------------------!
END SUBROUTINE GUESSA
!-----------------------------------------------------------------------------------------------!
